abstract type AbstractTDCone <: AbstractCone end

function workspacesize(::Type{<:AbstractTDCone}, n::Integer)
    @assert n == 3
    return 15
end

struct AbstractTDConeCache{C <: AbstractTDCone, T} <: AbstractCache{C}
    cone::C
    #
    # The factor of the barrier Hessian
    #
    #   P f''(p) Pᵀ = L Lᵀ
    #
    L::FMatrixView{T}
    #
    # The dual "shadow" iterate
    #
    #   d* = -f'(p)
    #
    sd::FVectorView{T}
    #
    # warm-start for computing the primal
    # "shadow" iterate p*, which solves
    #
    #   -f'(p*) = d
    #
    seed::FScalarView{T}
    #
    # the compensated determinant-chain scalars, cone-private
    # (pow: p, ρ, invdet; exp: ψ, invψ, lr, lrm1), written by
    # tddet! and read by the barrier consumers
    #
    d1::FScalarView{T}
    d2::FScalarView{T}
    d3::FScalarView{T}
    d4::FScalarView{T}
end

function cache(c::Caches{T}, i::Integer, cone::AbstractTDCone) where {T}
    data = cachedata(c, i)
    L    = reshape(view(data, 1:9), 3, 3)
    sd   = view(data, 10:12)
    seed = view(data, 13)
    d1   = view(data, 14)
    d2   = view(data, 15)
    d3   = view(data, 16)
    d4   = view(data, 17)
    return AbstractTDConeCache(cone, L, sd, seed, d1, d2, d3, d4)
end

# Compute the coefficient t in the rank-1 term
# tzzᵀ of the Tuncel scaling matrix M:
#
#   M = ⟨p, d⟩⁻¹   d  dᵀ
#     + ⟨δp,δd⟩⁻¹ δd δdᵀ
#     + t          z  zᵀ,
#
# where
#
#   δp = p - μ p*
#   δd = d - μ d*
#
function tdbfgs(
        L::AbstractMatrix{T},
        sp::AbstractVector{T},
        sd::AbstractVector{T},
        z::AbstractVector{T},
        p::AbstractVector{T},
        μv::T,
        μt::T,
        cache::AbstractTDConeCache,
        w::AbstractVector{T},
        Hw::AbstractVector{T},
        Hz::AbstractVector{T},
    ) where {T}
    t = zero(T)
    #
    # compute the gap direction
    #
    #   w = p* - μ* p
    #
    # where is the dual centrality parameter
    #
    #   μ* = ⟨p*, d*⟩ / ν.
    #
    copystatic!(w, sp, Val(3))
    axpystatic!(-μt, p, w, Val(3))
    #
    # compute the norm
    #
    #   ⟨w, f''(p) w⟩ = ‖Rᵀ w‖²
    #
    tdhessmul!(Hw, L, w, cache)
    wHw = dotstatic(w, Hw, Val(3))

    if wHw > 0
        #
        # compute the norm
        #
        #   ⟨z, f''(p) z⟩ = ‖Rᵀ z‖²
        #
        tdhessmul!(Hz, L, z, cache)
        fppzz = dotstatic(z, Hz, Val(3))
        #
        # compute the dot product
        #
        #   ⟨d*, z⟩
        #
        sdz = dotstatic(sd, z, Val(3))
        #
        # compute the dot product
        #
        #   ⟨w, f''(p) z⟩ = ⟨Rᵀ w, Rᵀ z⟩
        #
        wHz = dotstatic(Hw, z, Val(3))
        #
        # compute t:
        #
        #   t = μ ⟨z, f''(p) z⟩
        #     - μ ⟨d*,       z⟩² / ν
        #     - μ ⟨w, f''(p) z⟩² / ⟨w, f''(p) w⟩
        #
        # using compensated arithmetic
        #
        s1, e1 = twosum(fppzz, -sdz^2 / 3)
        s2, e2 = twosum(s1, -wHz^2 / wHw)
        t = μv * (s2 + (e1 + e2))
    end

    return t
end

# Assemble the Tuncel scaling matrix
#
#   M = ⟨p, d⟩⁻¹   d  dᵀ
#     + ⟨δp,δd⟩⁻¹ δd δdᵀ
#     + t          z  zᵀ,
#
# where
#
#   δp = p - μ p*
#   δd = d - μ d*
#
function tdscale!(
        H::AbstractMatrix{T},
        L::AbstractMatrix{T},
        sd::AbstractVector{T},
        seed::T,
        p::AbstractVector{T},
        d::AbstractVector{T},
        cache::AbstractTDConeCache,
        wrk::ConeWorkspace{T},
    ) where {T}

    sp = view(wrk.data,  1:3)
    z  = view(wrk.data,  4:6)
    w  = view(wrk.data,  7:9)
    δp = view(wrk.data, 10:12)
    δd = view(wrk.data, 13:15)

    next = one(T)
    #
    # compute the "determinant"
    #
    #   det(p)
    #
    # and store it in the cache
    #
    tddet!(cache, p)
    #
    # compute the "shadow" dual
    #
    #   d* = -f'(p)
    #
    tdbarrgrad!(sd, p, cache)
    lmulstatic!(-1, sd, Val(3))
    #
    # compute the analytic factor
    #
    #   f''(p) = R Rᵀ
    #
    flag = tdfact!(L, p, cache)

    if flag
        #
        # compute the "shadow" primal, solving d = -f'(p*)
        #
        next = tddualgrad!(sp, seed, d, cache)
        #
        # compute the centrality parameter
        #
        #   μ = ⟨p, d⟩ / ν
        #
        μv = dotstatic(p, d, Val(3)) / 3
        #
        # compute the dual centrality parameter
        #
        #   μ* = ⟨p*, d*⟩ / ν
        #
        μt = cdotstatic(sp, sd, Val(3)) / 3
        #
        # compute the cross-product
        #
        #   z = p × p*
        #
        crossstatic!(z, p, sp)
        #
        # compute the sine of the angle θ between p and p*:
        #
        #   ‖p × p*‖ / (‖p‖ ‖p*‖) = sin(θ)
        #
        # when this quantity is small, the iterate is close to
        # the central path and the term δd δdᵀ / ⟨δp, δd⟩ term in M
        # becomes innaccurate due to cancellation in the difference
        #
        #   δp = p - μ p*
        #
        # in this case, we fall back to the approximation
        #
        #   M ≈ μ f''(p)
        #
        nz  = normstatic(z, Val(3))
        np  = normstatic(p, Val(3))
        nsp = normstatic(sp, Val(3))

        if nz < eps(T) * (np * nsp + eps(T))
            tdgram!(H, L, μv, cache)
        else
            ldivstatic!(nz, z, Val(3))

            t = tdbfgs(L, sp, sd, z, p, μv, μt, cache, w, δp, δd)

            if t ≤ 0 || !isfinite(t)
                tdgram!(H, L, μv, cache)
            else
                copystatic!(δp, p, Val(3)); axpystatic!(-μv, sp, δp, Val(3))
                copystatic!(δd, d, Val(3)); axpystatic!(-μv, sd, δd, Val(3))

                 pd = 3μv
                δpd = cdotstatic(δp, δd, Val(3))

                gerstatic!(H,  d,  d, inv( pd), 1, Val(3))
                gerstatic!(H, δd, δd, inv(δpd), 1, Val(3))
                gerstatic!(H,  z,  z, t,        1, Val(3))
            end
        end
    end

    return flag, next
end

# Compute the Mehrotra corrector term
#
#   -d - σμ f'(p) - η,
#
# where η is the third-order correction
#
#   η = -½ f'''(p)[Δp, f''(p)⁻¹ Δd].
#
function tdcorr!(
        r::AbstractVector{T},
        L::AbstractMatrix{T},
        sd::AbstractVector{T},
        p::AbstractVector{T},
        d::AbstractVector{T},
        Δp::AbstractVector{T},
        Δd::AbstractVector{T},
        σμ::Real,
        cache::AbstractTDConeCache,
        wrk::ConeWorkspace{T},
    ) where {T}

    v =         view(wrk.data, 1:3)
    η =         view(wrk.data, 4:6)
    D = reshape(view(wrk.data, 7:15), 3, 3)
    #
    # solve for v in
    #
    #   f''(p) v =  Δd
    #
    # using the analytic factorization
    #
    #   f''(p) = R Rᵀ.
    #
    copystatic!(v, Δd, Val(3))
    tdhessldiv!(L, v, cache)
    #
    # compute the third-order correction
    #
    #   η = -½ f'''(p)[Δp, v]
    #
    tdbarrthird!(D, p, Δp, cache)
    mulstatic!(η, D, v, -0.5, 0, Val(3))
    #
    # compute the Mehrotra corrector term
    #
    #   -d - σμ f'(p) - η
    #
    copystatic!(r, d, Val(3))
    axpbystatic!(σμ, sd, -1, r, Val(3))
    axpystatic!(-1, η, r, Val(3))

    return r
end

function tdmaxsteps(
        p::AbstractVector{T},
        Δp::AbstractVector{T},
        d::AbstractVector{T},
        Δd::AbstractVector{T},
        cache::AbstractTDConeCache,
        wrk::ConeWorkspace{T},
    ) where {T}
    hip = tdboundprim(p, Δp, cache)
    hid = tdbounddual(d, Δd, cache)

    τp = nflast(hip) do τ
        tdjetprim(τ, p, Δp, cache)
    end

    τd = nflast(hid) do τ
        tdjetdual(τ, d, Δd, cache)
    end

    return τp, τd
end

#
# AbstractCone Interface
#

function degree(::AbstractTDCone, n::Integer)
    @assert n == 3
    return 3
end

function cachesize(::Type{<:AbstractTDCone}, n::Integer)
    @assert n == 3
    return 17
end

function scale!(H::AbstractMatrix{T}, p::AbstractVector{T}, d::AbstractVector{T}, cache::AbstractTDConeCache{C, T}, wrk::ConeWorkspace{T}) where {C, T}
    flag, seed = tdscale!(H, cache.L, cache.sd, cache.seed[], p, d, cache, wrk)
    cache.seed[] = seed
    return flag
end

function corr!(r::AbstractVector{T}, p::AbstractVector{T}, d::AbstractVector{T}, Δp::AbstractVector{T}, Δd::AbstractVector{T}, σμ::Real, cache::AbstractTDConeCache{C, T}, wrk::ConeWorkspace{T}) where {C, T}
    return tdcorr!(r, cache.L, cache.sd, p, d, Δp, Δd, σμ, cache, wrk)
end

function maxsteps(p::AbstractVector{T}, Δp::AbstractVector{T}, d::AbstractVector{T}, Δd::AbstractVector{T}, cache::AbstractTDConeCache{C, T}, wrk::ConeWorkspace{T}) where {C, T}
    return tdmaxsteps(p, Δp, d, Δd, cache, wrk)
end

function dualshadow!(sd::AbstractVector{T}, p::AbstractVector{T}, cache::AbstractTDConeCache{C, T}, ::ConeWorkspace{T}) where {C, T}
    tddet!(cache, p)
    tdbarrgrad!(sd, p, cache)
    lmulstatic!(-1, sd, Val(3))
    return true
end

function primalshadow!(sp::AbstractVector{T}, d::AbstractVector{T}, cache::AbstractTDConeCache{C, T}, ::ConeWorkspace{T}) where {C, T}
    tddualgrad!(sp, zero(T), d, cache)
    return true
end

# solve for x in
#
#   f''(p) x = b
#
# using a pre-computed factorization
#
#   P f''(p) Pᵀ = L Lᵀ
#
function tdhessldiv!(L::AbstractMatrix{T}, b::AbstractVector{T}, ::AbstractTDConeCache) where {T}
    b1, b2, b3 = b[1], b[2], b[3]

    y1 =  b3                              / L[1,1]
    y2 = (b1 - L[2,1] * y1)               / L[2,2]
    y3 = (b2 - L[3,1] * y1 - L[3,2] * y2) / L[3,3]

    z3 = y3                               / L[3,3]
    z2 = (y2 - L[3,2] * z3)               / L[2,2]
    z1 = (y1 - L[2,1] * z2 - L[3,1] * z3) / L[1,1]

    b[1] = z2
    b[2] = z3
    b[3] = z1
    return b
end

# apply the Hessian
#
#   f''(p) v = Pᵀ L Lᵀ P v
#
# using a pre-computed factorization
#
#   P f''(p) Pᵀ = L Lᵀ.
#
function tdhessmul!(out::AbstractVector{T}, L::AbstractMatrix{T}, v::AbstractVector{T}, ::AbstractTDConeCache) where {T}
    v1, v2, v3 = v[1], v[2], v[3]

    r1 = L[1,1] * v3 + L[2,1] * v1 + L[3,1] * v2
    r2 =               L[2,2] * v1 + L[3,2] * v2
    r3 =                             L[3,3] * v2

    t1 = L[1,1] * r1
    t2 = L[2,1] * r1 + L[2,2] * r2
    t3 = L[3,1] * r1 + L[3,2] * r2 + L[3,3] * r3

    out[1] = t2
    out[2] = t3
    out[3] = t1

    return out
end

# materialize the scaled Hessian
#
#   H = μ f''(p) = μ Pᵀ L Lᵀ P
#
# from a pre-computed factorization
#
#   P f''(p) Pᵀ = L Lᵀ.
#
function tdgram!(H::AbstractMatrix{T}, L::AbstractMatrix{T}, μ::T, ::AbstractTDConeCache) where {T}
    L11 = L[1,1]
    L21 = L[2,1]; L22 = L[2,2]
    L31 = L[3,1]; L32 = L[3,2]; L33 = L[3,3]

    H[3,3] += μ * L11^2
    H[1,1] += μ * (L21^2 + L22^2)
    H[2,2] += μ * (L31^2 + L32^2 + L33^2)

    H[1,3] = H[3,1] += μ * L21 * L11
    H[2,3] = H[3,2] += μ * L31 * L11
    H[1,2] = H[2,1] += μ * (L31 * L21 + L32 * L22)

    return H
end

include("exp.jl")
include("pow.jl")
include("utils.jl")
